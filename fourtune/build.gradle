plugins {
	id 'java'
	id 'org.springframework.boot' version '4.0.1'
	id 'io.spring.dependency-management' version '1.1.7'
}

group = 'com.fourtune'
version = '0.0.1-SNAPSHOT'
description = 'fourtune'

java {
	toolchain {
		languageVersion = JavaLanguageVersion.of(25)
	}
}

ext {
	querydslVersion = '5.0.0'
	jwtVersion = '0.12.3'
}

configurations {
	compileOnly {
		extendsFrom annotationProcessor
	}
}

repositories {
	mavenCentral()
}

dependencies {
	// Spring Boot Starters
	implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
	implementation 'org.springframework.boot:spring-boot-starter-data-redis'
	implementation 'org.springframework.boot:spring-boot-starter-data-elasticsearch'
	implementation 'org.springframework.boot:spring-boot-starter-security'
	implementation 'org.springframework.boot:spring-boot-starter-validation'
	implementation 'org.springframework.boot:spring-boot-starter-web'
	implementation 'org.springframework.boot:spring-boot-starter-websocket'
	implementation 'org.springframework.boot:spring-boot-starter-actuator'
	
	// Event (Kafka는 추후 확장)
	implementation 'org.springframework.boot:spring-boot-starter-kafka'
	
	// JWT
	implementation "io.jsonwebtoken:jjwt-api:${jwtVersion}"
	runtimeOnly "io.jsonwebtoken:jjwt-impl:${jwtVersion}"
	runtimeOnly "io.jsonwebtoken:jjwt-jackson:${jwtVersion}"
	
	// QueryDSL
	implementation "com.querydsl:querydsl-jpa:${querydslVersion}:jakarta"
	annotationProcessor "com.querydsl:querydsl-apt:${querydslVersion}:jakarta"
	annotationProcessor "jakarta.annotation:jakarta.annotation-api"
	annotationProcessor "jakarta.persistence:jakarta.persistence-api"
	
	// AWS S3 (나중에 이미지 업로드 기능 만들 때 추가)
	// implementation 'io.awspring.cloud:spring-cloud-aws-starter-s3:3.1.0'
	
	// JSON
	implementation 'com.fasterxml.jackson.datatype:jackson-datatype-jsr310'
	implementation 'com.fasterxml.jackson.core:jackson-databind'
	
	// Lettuce (Redis Client)
	implementation 'io.lettuce:lettuce-core'
	
	// Database
	runtimeOnly 'org.postgresql:postgresql'
	
	// Lombok
	compileOnly 'org.projectlombok:lombok'
	annotationProcessor 'org.projectlombok:lombok'
	
	// Test Dependencies
	testImplementation 'org.springframework.boot:spring-boot-starter-test'
	testImplementation 'org.springframework.security:spring-security-test'
	testImplementation 'org.testcontainers:junit-jupiter'
	testImplementation 'org.testcontainers:postgresql'
	testImplementation 'org.testcontainers:elasticsearch'
	testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
}

tasks.named('test') {
	useJUnitPlatform()
}

// QueryDSL 설정 (Annotation Processor 방식)
def generatedDir = layout.buildDirectory.dir("generated/sources/annotationProcessor/java/main")

tasks.withType(JavaCompile).configureEach {
	options.generatedSourceOutputDirectory = generatedDir
}

clean {
	delete layout.buildDirectory.dir("generated")
}
